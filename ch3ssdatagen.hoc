{load_file("ch3ppchannel.ses")}
objref tobj
tobj = new List("ChannelBuild")
ch3pp_cbindex_ = tobj.count-1
objref tobj

begintemplate ch3datagen
public soma1, ch, stim
create soma1
objref ch, stim
external ch3pp_cbindex_

proc init() {
	soma1 {
		ch = new ch3pp(.5)
		ch.Nsingle = $1
		stim = new SEClamp(.5)
		stim.dur1 = 1e9
		stim.amp1 = 20
		stim.rs = .1
	}
}
endtemplate ch3datagen

obfunc ch3ssdata() { local sav localobj m, tvec, ov, cb, nil, ff, xmd, ymd
	ff = $o5 // fitnes function
	xmd = new Vector()
	ymd = new Vector()
	xmd.indgen(0,tstop,0.1)

	cb = ChannelBuild[ch3pp_cbindex_]
	m = new ch3datagen($1)
	ov = new Vector()
	cb.aliases.tauC2O.A = exp($o4.x[0])
	cb.aliases.tauCC2.A = exp($o4.x[1])
	m.soma1 {
		ov.record(&m.ch.O, $o3)
		ymd.record(&m.ch.O, xmd)
	}
        sav = cvode.active()
        cvode.active(0)
	dt = 0.025
        finitialize(v_init)
	cb.ks.rseed($2*100000)
	run()
	// for some reason the BG/P clears ymd and ov when m is destroyed
	ymd = ymd.c()
	ov = ov.c()
        m = nil
        cvode.active(sav)
        finitialize()
	sav = ff.n_chdat
	ymd.mul(1/$1)
	ff.set_data(xmd,ymd)
	execute("setxy()", ff)
//	ff.n_chdat = sav
	return ov.mul(1/$1)
}

proc test() {localobj vv, tvec, pvec
	pvec = new Vector(0)
	pvec.append($3, $4)
	tvec = new Vector()
	tvec.indgen(0,tstop,.1)
	vv = ch3ssdata($1, $2, tvec, pvec)
	vv.printf()
}

//test(1000, 1, 4, 2)

